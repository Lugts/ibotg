#!/usr/bin/tclsh

# ibotg  IRC bot to Telegram: a bot that allows using Telegram from IRC
# Copyright (C) 2015 E. Bosch

# MIT/X11 License

# Permission is hereby granted, free of charge, to any person obtaining
# a copy of this software and associated documentation files (the
# "Software"), to deal in the Software without restriction, including   
# without limitation the rights to use, copy, modify, merge, publish,
# distribute, sublicense, and/or sell copies of the Software, and to
# permit persons to whom the Software is furnished to do so, subject to
# the following conditions:

# The above copyright notice and this permission notice shall be
# included in all copies or substantial portions of the Software.

# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
# EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
# MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
# IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY
# CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,
# TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
# SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.

package require picoirc

proc leer_conf { co } \
{
   upvar #0 argv arg   argc argn
   upvar 1 $co conf

   # configuration file key=value
   # loaded in array co

   if { $argn==1 } { set config $arg } else { set config config }
   set cf [ open $config ]
   while { [ gets $cf lin ] >= 0 } \
   {
      if { [ string range [ string trim $lin ] 0 0 ] == "#" } { continue }
      set lconf [ split $lin = ]
      lassign $lconf key val
      set conf([ string trim $key]) [ string trim $val ]
   }
}

proc leer_lista { flista nc2nl nl2nc canales media } \
{
   upvar 1 $nc2nl c2l   $nl2nc l2c   $canales chans

   # nc2nl -> array de nick corto a nick largo
   # nl2nl -> array de nick largo a nick corto
   # canales -> array lista de canales de cada nick corto (sin prefijo)

   set li [ open $flista ]
   while { [ gets $li lin ] >= 0 } \
   {
      if { [ string range [ string trim $lin ] 0 0 ] == "#" } { continue }
      set trio [ split $lin : ]
      lassign $trio nc nl grupos
      if { $grupos=="" && $nc!=$media } { continue }
      set c2l($nc) $nl
      set l2c($nl) $nc
      set chans($nc) [ split $grupos , ]
   }
}

proc parse { string hora to from mensaje tg_name tg_echo base media } \
{
   upvar #0 last lt   mpend pend
   upvar 1 $hora ho   $to t   $from f   $mensaje msg

   set items [ split $string "\x1E" ]
   lassign $items id mi hi ho f t msg
   if { $id!="9ZxtQy" } \
   {
      if { $id=="3kdy5F" } \
      {
         append url $base [ string range $msg [ string last / $msg ] end ]
         if { [ info exists pend($mi) ] } \
         {
            lassign $pend($mi) f t m
            unset pend($mi)
            append m " " $url
         } else \
         {
            set t $tg_name
            set f $media
            append m {[} $mi {] } $url
         }
         set msg $m
         return 1
      }
      if { $lt } { comm_last $string 2 }
      if { [ regexp -lineanchor {^\[[^]]+\] .*$} $string ] } { return 2 } \
      else { return 0 }
   }
   if { $f==$tg_name && !$hi && !$tg_echo } { return 0 }
   if { $mi>0 && !$hi } { set pend($mi) [ list $f $t $msg ]; return 2 }
   if { [ string index $msg 0 ] == "/" } { append me " " $msg; set msg $me } 
   return 1
}

proc tg2irc { tg ir nl2nc pr master root tg_name tg_echo delay base media } \
{
   upvar 1 $ir irc   $nl2nc l2c

   set c [ gets $tg linea ]
   after $delay
   if { $c<0 } \
   {
      if { [ eof $tg ] } { exit }
   } else \
   {
      set res [ parse $linea hora to from mensaje $tg_name $tg_echo $base $media ]
      if { $res==1 } \
      {
         if { $to!=$tg_name } { set target $pr$to } \
         else { set target $master }
         if { [ info exists l2c($from) ] } { set usu $l2c($from) } \
         else { set usu unknown; append me {[} $from {] } $mensaje; set mensaje $me }
         picoirc::post $irc($usu) $target $mensaje
      } \
      elseif { $res==0 } { picoirc::post $irc($root) $master [ string range $linea 0 400 ] }
   }
}

proc irc2tg { irc estado args } \
{
   upvar #0 tg tp   ncirc nci   nc2nl c2l   canales chans   co conf   njd noj
   set pr $conf(prefix)
   set mast $conf(master)
   set r00t $conf(root)

   switch -exact -- $estado \
   {
      "debug" \
      {
         lassign $args tipo mensaje
         if { $noj($irc) && \
              $tipo=="read" && \
              [ string first MOTD $mensaje ] != -1 \
            } \
            {  
               foreach chan $chans($nci($irc)) { picoirc::post $irc "" "/join $pr$chan" }
               set noj($irc) 0
            }
      }
      "chat" \
      {
         lassign $args target sender mensaje tipo

         # $nci($irc) -> nick de la conexion (destino)
         # sender -> nick remitente
         # target -> nick destino (privado) o canal

         set pre   [ string index $target 0 ]
         set grupo [ string range $target 1 end ]
         if { $sender==$mast } \
         {
            if { $nci($irc)==$r00t } \
            { 
               if { $pre==$pr }             { puts $tp "msg $grupo $mensaje" } \
               elseif { $target==$r00t } \
               {
                  if { [ string index $mensaje 0 ] == "!" } { command $mensaje } \
                  else { puts $tp $mensaje }
               }
            } \
            elseif { $nci($irc)==$target } { puts $tp "msg $c2l($target) $mensaje" }	
         }
      }
      "close" { exit }
   }
}

proc command { com } \
{
   set lc [ split $com ]
   set c0 [ lindex $lc 0 ]
   switch -exact -- $c0 \
   {
      "!last" { comm_last $lc 1 }
   }
}

proc comm_last { line fase } \
{
   upvar #0 tg tp   last lt   arg ar   nc2nl c2l   co(last_add) add

   if { $fase==1 } \
   {   
      set lt 1
      set ar [ lsearch -inline -start 1 -not $line {} ]
      puts $tp dialog_list
   } \
   elseif { $fase==2 } \
   {
      if { [ info exists c2l($ar) ] } \
      {
         set arl_ $c2l($ar)
         set arl [ string map { _ " " } $arl_ ]
      } \
      else { set arl_ [ set arl $ar ] }         
      if { [ regexp -lineanchor "^(Chat|User) $arl: (\[0-9\]+) unread$" $line - -> msgs ] } \
      {
         set lt 0
         set msgs [ expr $msgs + $add ]
         puts $tp "history $arl_ $msgs"
      }
   }   
}


set last 0
set arg ""
array set co {}
array set mpend {}

leer_conf co

leer_lista $co(contact_file) nc2nl nl2nc canales $co(media)

foreach ni [ array names nc2nl ] \
{
   set irc($ni) [ picoirc::connect irc2tg $ni $co(pass) "irc://$co(serv):$co(port)" ] 
   set ncirc($irc($ni)) $ni
   set njd($irc($ni)) 1
   after $co(delay_join)
}

set tg [ open |$co(bintg) w+ ]
fconfigure $tg -blocking 0 -buffering line -encoding utf-8
fileevent $tg readable { tg2irc $tg irc nl2nc $co(prefix) $co(master) $co(root) $co(tg_name) \
                                $co(tg_echo) $co(delay_line) $co(media_base) $co(media) }

vwait forever
